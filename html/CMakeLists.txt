cmake_minimum_required(VERSION 3.3)

project(lci C)

# emcscripten is needed for the html version
if(NOT EMSCRIPTEN)
	add_custom_target(html echo 'ERROR: not building with emscripten' && echo 'Use emcmake cmake .. make html to build the html target' && false)
	return()
endif()

# make_dparser needs to be build with a normal build before building with emscripten
if(NOT EXISTS "${CMAKE_CURRENT_BINARY_DIR}/../dparser/make_dparser")
	message(FATAL_ERROR "'make_dparser' needs to be created before running emcmake.\nPlease build lci with a normal compiler first.")
endif()

# copy all html/* files to the 'dist' dir (using configure_file, to create dependencies)
file(GLOB_RECURSE HTML_SOURCES RELATIVE ${CMAKE_CURRENT_SOURCE_DIR} LIST_DIRECTORIES false *.html *.js *.css)
foreach(FILE ${HTML_SOURCES})
	configure_file(${FILE} ${CMAKE_CURRENT_BINARY_DIR}/dist/${FILE} COPYONLY)
endforeach()

# the html target copies the files generated by emcc under dist/
add_custom_target(html ALL
	DEPENDS lci
	COMMAND ${CMAKE_COMMAND} -E copy ../lci.wasm ../lci.js ../lci.data dist/
)